#!/bin/bash

light="#DCBCBC"
light_highlight="#C79999"
mid="#B97C7C"
mid_highlight="#A25050"
dark="#8F2727"
dark_highlight="#7C0000"

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 30' size 7.5, 5
set output "states.eps"

set pm3d map implicit at s

set title ""

set border 0

set lmargin screen 0.0
set rmargin screen 1.0
set bmargin screen 0.0
set tmargin screen 1.0

set xtics nomirror scale 0
set format x ''
set xrange[-15:15]

set ytics nomirror scale 0
set format y ''
set yrange[-5:3]

set zlabel ''
set zrange[-20:7]

unset cbtics
unset colorbox

set palette rgbformulae 8, 9, 9

set samples 400
set isosamples 400

set datafile separator ','

splot  0.5 * (x * exp(-y) )**2 + y \
     + 0.5 * (exp(y) / 3)**2 - y \
     + log(0.0752) + log(0.02005) + 8.732925977 notitle, \
'output.csv' using 9:(log(\$8)):(7) every ::600::900 w points pt 7 ps 3 lc rgb "$light" notitle, \
'output.csv' using 9:(log(\$8)):(7) every ::600::900 w lines lw 3 lc rgb "$light" notitle, \
'output.csv' using 9:(log(\$8)):(7) every ::600::900 w points pt 7 ps 2 lc rgb "$dark" notitle
END

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 30' size 7.5, 5
set output "trace.eps"

set title ""

set border 0

set lmargin screen 0.0
set rmargin screen 1.0
set bmargin screen 0.0
set tmargin screen 1.0

set xtics nomirror scale 0
set format x ''
set xrange[0:300]

set ytics nomirror scale 0
set format y ''
set yrange[-5:3]

set datafile separator ','

plot \
'output.csv' using 0:(log(\$8)) every ::600::900 w points pt 7 ps 3 lc rgb "$light" notitle, \
'output.csv' using 0:(log(\$8)) every ::600::900 w lines lw 3 lc rgb "$light" notitle, \
'output.csv' using 0:(log(\$8)) every ::600::900 w points pt 7 ps 2 lc rgb "$dark" notitle
END

exit
