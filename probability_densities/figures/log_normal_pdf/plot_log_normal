#!/bin/bash

light="#DCBCBC"
light_highlight="#C79999"
mid="#B97C7C"
mid_highlight="#A25050"
dark="#8F2727"
dark_highlight="#7C0000"

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 4
set output "normal_pdf.eps"

set border 3 lw 2

set title ""

set lmargin screen 0.125
set rmargin screen 0.875

set xtics out nomirror
set xlabel 'x'
set xrange[-4:8]

set ytics scale 0
set ylabel "Probability Density Function"
set format y ''

mu = log(10) - 0.5
sigma = 1

normal(x, mu, sigma) = exp(-0.5 * ((x - mu) / sigma)**2) / (sqrt(2 * pi * sigma**2))

set samples 500

plot \
0.0005 * exp(x) w l lt 1 dt '.' lw 8 lc rgb "gray" notitle, \
normal(x, mu, sigma) w l lt 1 lw 8 lc rgb "$dark" notitle

END

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 4
set output "log_normal_pdf.eps"

set border 3 lw 2

set title ""

set lmargin screen 0.125
set rmargin screen 0.875

set xtics out nomirror
set xlabel 'x'
set xrange[0:50]

set ytics scale 0
set ylabel "Probability Density Function"
set format y ''

mu = log(10) - 0.5
sigma = 1

log_normal(x, mu, sigma) = exp(-0.5 * ((log(x) - mu) / sigma)**2) / (x * sqrt(2 * pi * sigma**2))

set samples 500

plot log_normal(x, mu, sigma) w l lt 1 lw 8 lc rgb "$dark" notitle

END
